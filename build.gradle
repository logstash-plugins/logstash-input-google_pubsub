import java.nio.file.Files
import java.nio.file.Paths
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group "org.logstash.inputs"
version Files.readAllLines(Paths.get("version")).first()

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.cloud:google-cloud-pubsub:1.115.1"

    implementation "com.fasterxml.jackson.core:jackson-core:2.1.3" // is it needed?
    implementation "com.google.api:api-common:2.1.2"
    implementation "com.google.api:gax:2.8.1"
    implementation "com.google.api:gax-grpc:2.8.1"
    implementation "com.google.api.grpc:grpc-google-cloud-pubsub-v1:1.19.1" // is it needed?
    implementation "com.google.api.grpc:proto-google-cloud-pubsub-v1:1.97.1"
    implementation "com.google.api.grpc:proto-google-common-protos:2.7.1"
    implementation "com.google.api.grpc:proto-google-iam-v1:1.2.0"
    implementation "com.google.auth:google-auth-library-credentials:1.3.0"
    implementation "com.google.auth:google-auth-library-oauth2-http:1.3.0"
    implementation "com.google.auto.value:auto-value:1.4" // is it needed ?
    implementation "com.google.cloud:google-cloud-core:1.37.1" // is it needed?
    implementation "com.google.cloud:google-cloud-core-grpc:1.37.1" // is it needed?
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.google.code.gson:gson:2.8.9"
    implementation "com.google.errorprone:error_prone_annotations:2.10.0"
    implementation "com.google.guava:guava:31.0.1-jre"
    implementation "com.google.http-client:google-http-client:1.41.0"
    implementation "com.google.http-client:google-http-client-jackson2:1.19.0" // is it needed?
    implementation "com.google.protobuf:protobuf-java:3.19.2"
    implementation "com.google.protobuf:protobuf-java-util:3.19.2"
    implementation "com.google.protobuf:protobuf-lite:3.0.1" // is it needed?
    implementation "commons-codec:commons-codec:1.15"
    implementation "commons-logging:commons-logging:1.2"
    implementation "io.grpc:grpc-auth:1.43.2"
    implementation "io.grpc:grpc-context:1.43.2"
    implementation "io.grpc:grpc-core:1.43.2"
    implementation "io.grpc:grpc-netty-shaded:1.43.2"
    implementation "io.grpc:grpc-protobuf:1.43.2"
    implementation "io.grpc:grpc-protobuf-lite:1.43.2"
    implementation "io.grpc:grpc-stub:1.43.2"
    implementation "io.opencensus:opencensus-api:0.28.0"
    implementation "io.opencensus:opencensus-contrib-grpc-metrics:0.12.3" // is it needed?
    implementation "joda-time:joda-time:2.9.2" // is it needed?
    implementation "org.apache.httpcomponents:httpclient:4.5.13"
    implementation "org.apache.httpcomponents:httpcore:4.4.15"
    implementation "org.threeten:threetenbp:1.5.2"
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-input-google_pubsub_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
            w << "require_jar(\'${project.group}\', \'${project.name}\', \'${project.version}\')\n"
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.allDependencies.each { dep ->
            File f = configurations.runtimeClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${project.name}/${project.version}/${project.name}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
    }
}

vendor.dependsOn(jar, generateGemJarRequiresFile)
