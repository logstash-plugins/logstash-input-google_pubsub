import java.nio.file.Files
import java.nio.file.Paths
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

plugins {
  id 'java'
  id 'idea'
  id 'maven-publish'
}

group "org.logstash.inputs"
version Files.readAllLines(Paths.get("version")).first()

repositories {
    mavenCentral()
}

dependencies {
    constraints {
        implementation('com.google.protobuf:protobuf-java:3.25.5')
        implementation('com.google.protobuf:protobuf-java-util:3.25.5')
        implementation('com.google.protobuf:protobuf-javalite:4.29.3')

        // All gRPC components must be at the same version to align on the same API interfaces
        implementation('io.grpc:grpc-netty-shaded:1.75.0')
        implementation('io.grpc:grpc-api:1.75.0')
        implementation('io.grpc:grpc-core:1.75.0')
        implementation('io.grpc:grpc-context:1.75.0')
        implementation('io.grpc:grpc-stub:1.75.0')
        implementation('io.grpc:grpc-protobuf:1.75.0')
        implementation('io.grpc:grpc-protobuf-lite:1.75.0')
        implementation('io.grpc:grpc-alts:1.75.0')
        implementation('io.grpc:grpc-auth:1.75.0')
        implementation('io.grpc:grpc-grpclb:1.75.0')
        implementation('io.grpc:grpc-googleapis:1.75.0')
        implementation('io.grpc:grpc-services:1.75.0')
        implementation('io.grpc:grpc-xds:1.75.0')
        implementation('io.grpc:grpc-util:1.75.0')
    }
    // Core dependency - transitive dependencies are automatically resolved
    implementation "com.google.cloud:google-cloud-pubsub:1.124.1"
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file("lib/logstash-input-google_pubsub_jars.rb")
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
                def id = artifact.id.componentIdentifier
                if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                    w << "require_jar(\'${id.group}\', \'${id.module}\', \'${id.version}\')\n"
                }
            }

            w << "require_jar(\'${project.group}\', \'${project.name}\', \'${project.version}\')\n"
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.incoming.artifacts.artifacts.each { artifact ->
            def id = artifact.id.componentIdentifier
            if (id instanceof org.gradle.api.artifacts.component.ModuleComponentIdentifier) {
                String group = id.group
                String name = id.module
                String version = id.version
                String groupPath = group.replaceAll('\\.', '/')
                File newJarFile = file("${vendorPathPrefix}/${groupPath}/${name}/${version}/${name}-${version}.jar")
                if (!newJarFile.parentFile.exists()) {
                    newJarFile.parentFile.mkdirs()
                }
                Files.copy(artifact.file.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
            }
        }

        // Copy the project jar into vendor/jar-dependencies
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${project.name}/${project.version}/${project.name}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
    }
}

vendor.dependsOn(jar, generateGemJarRequiresFile)
